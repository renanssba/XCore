fade_music 0 0.7
fade_out 0.7
bg "office"
wait 0.5
fade_in 0.7

set_var shop_level 1

//play_music "graciel_intro" "graciel_loop"

goto_script "char_right_enter" "Lara face"
say_auto "Lara" "Olá, cadete. Como posso ajudá-lo?"
goto store_choices


waypoint store_loop
say_auto "Lara" "Quer fazer mais algum negócio?"
waypoint store_choices
choices "Comprar" buy "Vender" sell "Sair" end


waypoint buy
goto_script "shop_movement" "open_buy_screen"
show_shop true "buy"
goto_script "shop_movement" "close_buy_screen"

if item_id != -1
  if item_price <= money
    say_auto "Lara" "Você quer comprar <color=#D73022>\vsn(item_name)</color> por <sprite=\qAttributes\q index=4><color=#D73022>\vsn(item_price)</color>?"
    choices "Sim" comprar_sim "Não" store_loop
  else
    say "Me desculpe, parece que você não tem o suficiente para comprar este item."
  endif
else
  goto store_loop
endif
goto store_loop


waypoint sell
if #inventory_empty == 1
  say "Você não tem nenhum item para vender."
goto store_loop
endif

goto_script "shop_movement" "open_buy_screen"
show_shop true "sell"
goto_script "shop_movement" "close_buy_screen"
if item_id != -1
  say_auto "Lara" "Você quer vender <color=#D73022>\vsn(item_name)</color> por <sprite=\qAttributes\q index=4><color=#D73022>\vsn(item_price)</color>?"
  choices "Sim" vender_sim "Não" store_loop
else
  goto store_loop
endif
goto store_loop



waypoint comprar_sim
subtract_var money item_price
update_ui
add_item item_id 1
check_buy_all_achievement
play_sfx "ui_money"
goto store_loop


waypoint vender_sim
add_var money item_price
update_ui
play_sfx "ui_money"
check_sell_wrong_item
add_item item_id -1 false
say "Lara" "Obrigada, iremos encontrar outro uso para esse recurso."
goto store_loop

// waypoint

waypoint end
say "Lara" "Estarei à disposição sempre que precisar."

fade_music 0 0.7
fade_out 0.7
char_reset_all
bg null
wait 0.5
fade_in 0.7
if daytime == 2
	play_music "intermission_night_intro" "intermission_night_loop"
else
	play_music "intermission_intro" "intermission_loop"
endif

